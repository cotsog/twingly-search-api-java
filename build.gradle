apply plugin: "idea"
apply plugin: "java"
apply plugin: "groovy"
version = "0.0.3"
sourceCompatibility = 1.7
targetCompatibility = 1.7

ext {
    sourceEncoding = "UTF-8"
    spockVersion = "1.0-groovy-2.3"
    groovyVersion = "2.3.3"
    junitVersion = "4.12"
    cglibVersion = "3.2.0"
    objenesisVersion = "2.2"
    jaxbVersion = "2.2.12"

    libs = [
            jaxb     : "javax.xml.bind:jaxb-api:$jaxbVersion",
            junit    : "junit:junit:$junitVersion",
            groovy   : "org.codehaus.groovy:groovy-all:$groovyVersion",
            spock    : "org.spockframework:spock-core:$spockVersion",
            cglib    : "cglib:cglib-nodep:$cglibVersion",
            objenesis: "org.objenesis:objenesis:$objenesisVersion"

    ]

    javadocFolder = "doc"

    sharedManifest = manifest {
        attributes("Implementation-Title": 'Twingly Search API Java',
                "Implementation-Version": version)
    }
}

repositories {
    mavenCentral()
    jcenter()
    mavenLocal()
}
dependencies {
    compile 'org.apache.httpcomponents:httpclient:4.5.1'
    compile libs.jaxb
    testCompile libs.junit
    testCompile libs.groovy
    testCompile libs.spock
    testRuntime libs.cglib
    testRuntime libs.objenesis
}

test {
    testLogging.showStandardStreams = true
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
    }
}
//create a single Jar with all dependencies
task generateOneJar(type: Jar) {
    manifest = project.manifest {
        from sharedManifest
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task generateJavadocs(type: Javadoc) {
    source = sourceSets.main.allJava
    options.memberLevel = JavadocMemberLevel.PROTECTED
    classpath = configurations.compile
    destinationDir = file(javadocFolder)
}

tasks.withType(JavaCompile.class) {
    options.deprecation = false
    options.warnings = false
    options.encoding = sourceEncoding
    options.compilerArgs = ["-Xmaxerrs", "20", "-Xmaxwarns", "0", "-Xlint:all"]
}